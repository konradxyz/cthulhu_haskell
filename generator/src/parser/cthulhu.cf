-- programs ------------------------------------------------

RealProgram.    Program ::= [TopDef] ;

FnDef.             TopDef ::= Ident "::" Type ":" Ident [Ident] "=" Expr ";" ;
FnDefTemplate.     TopDef ::= Ident "<" [DeclTemplateType] ">" "::" Type ":" 
                              Ident [Ident] "=" Expr ";" ;
DataTypeDef.       TopDef ::= "data" Ident [Ident] "=" [DataVariant] ";" ;  
separator TopDef "" ;
separator Ident "" ;


RealDeclTemplateType.   DeclTemplateType ::= Ident ;
separator DeclTemplateType  ",";


RealDataVariant. DataVariant ::= Ident [DataVariantField] ;
separator nonempty DataVariant "|" ;

IdDataVariantField.   DataVariantField ::= Ident ;
TypeDataVariantField. DataVariantField ::= "(" Type ")" ;
separator DataVariantField "";

RealSimpleType. SimpleType ::= "(" Type ")" ;
IdSimpleType.   SimpleType ::= Ident ;

ComplexType.  Type1 ::= Ident [SimpleType] ;

FnType.           Type  ::= Type1 "->" Type ;

separator SimpleType "" ;
coercions Type 1;

RealTypeListElem. TypeListElem ::=  Type ;
separator nonempty TypeListElem ",";




-- Expressions ---------------------------------------------
EVar.              Expr2 ::= Ident ;
EVarTemplate.      Expr2 ::= Ident "<" [TypeListElem] ">" ;

ELitInt.   Expr2 ::= Integer ;

EApply.    Expr1 ::= Expr1 Expr2 ;

ELet.      Expr ::= "let" Ident "=" Expr1 "in" Expr ;

EIfElse.     Expr ::= "if" Expr1 "then" Expr "else" Expr ;
ECase.       Expr ::= "case" Expr1 "of" [CaseVariant] ;



RealCaseVariant. CaseVariant ::= Ident [Ident] "->" Expr ;
terminator CaseVariant ";" ;

coercions  Expr 2 ;



-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;

